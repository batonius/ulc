use terms::{Term, Variable, RcTerm};
use builtin::BuiltinType;
use std::str::FromStr;
use types::{TermType, RcTermType};

grammar;

pub Term: RcTerm = {
    <Abs>,
    <If>,
    <Appl>,
};

SimpleTerm: RcTerm = {
    "(" <Term> ")",
    Num => Term::num_lit_rc(<>),
    Bool => Term::bool_lit_rc(<>),
    Builtin => Term::builtin_rc(<>, vec![]),
    Var => Term::var_rc(<>),
};

Num: isize = {
    r"[0-9]+" => isize::from_str(<>).unwrap()
};

Bool: bool = {
    "true" => true,
    "false" => false
};

Builtin: BuiltinType = {
    "+" => BuiltinType::Add,
    "-" => BuiltinType::Sub,
    "*" => BuiltinType::Mul,
    "/" => BuiltinType::Div,
    "=" => BuiltinType::Eq,
};

Var: Variable = {
    <n:r"[a-z][a-zA-Z0-9]*"> <t:( ":" <Type>)?> => {
        if let Some(t) = t {
            Variable::with_type(n, t)
        } else {
            Variable::new(n)
        }
    }
};

Abs: RcTerm = {
    r"\\" <v:Var> "." <t:Term> => Term::abs_rc(v, t)
};

Appl: RcTerm = {
    Appl SimpleTerm => Term::appl_rc(<>),
    <SimpleTerm>
};

If: RcTerm = {
    "if" <Term> "then" <Term> "else" <Term> => Term::if_rc(<>)
};

SimpleType: RcTermType = {
    "(" <Type> ")",
    "Int" => TermType::new_int(),
    "Bool" => TermType::new_bool(),
    r"[A-Z][a-zA-Z0-9]*" => TermType::new_var(<>)
};

pub Type: RcTermType = {
    <SimpleType> "->" <Type> => TermType::new_arrow(<>),
    <SimpleType>
};
